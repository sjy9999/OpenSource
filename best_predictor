#!/usr/bin/env bash

# CITS4407 Open Source   assignment 2    best_predictor
# Name: Junyi Sun
# Student Number: 24382608
# Date:2024  May 16th

# this is a good way to show the result    easy for me to debug
# echo "This is the do not have the same number of cells:"
# cat

# Note!!!!!!!!!!!!!!!!!!!!!!!!!
# The end of this section corresponds to the end of the first step. 
# If you uncomment the comments at the bottom of the first script and change the first script to automatically generate "output.tsv", 
# then this script will read "output.tsv", achieving complete automation of the program. 
# In that case, you can comment out the code section below that checks for provided parameters and retrieves the file name.

# # if there is no input  then default is output.tsv     如果没有指定文件，则默认为 output.tsv
# file="${1:-output.tsv}"  

# to check wheather 检查是否提供了参数
if [ $# -eq 0 ]; then
    echo "Usage: $0 <input_file.tsv>"
    exit 1
fi

# get the name of the input file 获取文件名
file="$1"

# this is the start of awk
awk -F'\t' '
function abs(x) { return x < 0 ? -x : x; }  # here I get function to get absolute value     定义一个绝对值函数

BEGIN {
    # here I Initia data and set column num                初始化数据类型和对应的列号
    types["Homicide Rate"] = 6;
    types["GDP"] = 4;
    types["Population"] = 5;
    types["Life Expectancy"] = 7;

    # here I set output order                   定义输出顺序
    order[1] = "Homicide Rate";
    order[2] = "GDP";
    order[3] = "Population";
    order[4] = "Life Expectancy";
    max_corr = 0;  
    #  Here I set the maximum correlation coefficient is 0                  初始化最大相关系数

}

{
    # here I only consider rows that have Cantril scores     make sure these col(4 5 6 7 8) have the num              只考虑有Cantril得分的行
    if ($8 != "" && $4 != "" && $5 != "" && $6 != "" && $7 != "") {
        country = $2;
        cantrilEntries[country]++;
        tempData[country][cantrilEntries[country]] = $4 " " $5 " " $6 " " $7 " " $8;
    }
}

END {
    # Iterate all country and check larger than 3 Cantril                遍历每个国家，检查Cantril得分的数据点是否足够
    for (country in cantrilEntries) {
        if (cantrilEntries[country] >= 3) {
            # here I get sufficient for Cantril   type is like population GDP etc                 该国家的Cantril得分数据点足够，处理数据
            for (t in types) {
                # at start   make them all 0
                sum_x = sum_y = sum_xy = sum_x2 = sum_y2 = n = 0;
                for (i = 1; i <= cantrilEntries[country]; i++) {
                    split(tempData[country][i], values, " ");
                    x = values[types[t] - 3];
                    y = values[5];  # Index of Cantril make sure correct          Cantril值的索引 确保正确
                    if (x != "" && y != "") {  # Ensure x and y are not empty          确保x和y非空
                        sum_x += x;
                        sum_y += y;
                        sum_xy += x * y;
                        sum_x2 += x * x;
                        sum_y2 += y * y;
                        n++;
                    }
                }
                if (n > 0) {
                    numerator = n * sum_xy - sum_x * sum_y;
                    denominator = sqrt((n * sum_x2 - sum_x^2) * (n * sum_y2 - sum_y^2));
                    if (denominator != 0) {
                        correlation = numerator / denominator;
                        # add all the correlation
                        corrSum[t] += correlation;
                        corrCount[t]++;
                    }
                }
            }
        }
    }

    # Calculate and print the average correlation for each type     from 1 to 4          计算并打印每种类型的平均相关系数
    for (i = 1; i <= 4; i++) {
        t = order[i];
        if (corrCount[t] > 0) {
            avgCorr = corrSum[t] / corrCount[t];
            printf "Mean correlation of %s with Cantril Ladder is %.3f\n", t, avgCorr;
            # Check if is the maximum correlation          检查是否为最大的相关系数
            if (abs(avgCorr) > abs(max_corr)) {
                max_corr = avgCorr;
                max_type = t;
            }
        } else {
            print "Data is not enough or division by zero" t;
        }
    }
    # Print out the maximum correlation and its corresponding type         打印出最大的相关系数和对应的类型
    printf "Most predictive mean correlation with the Cantril ladder is %s (r = %.3f)\n", max_type, max_corr;
}' "$file"

# }' "$file"
# }'